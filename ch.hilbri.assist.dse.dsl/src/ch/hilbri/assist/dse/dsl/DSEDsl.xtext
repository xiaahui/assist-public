grammar ch.hilbri.assist.dse.dsl.DSEDsl with org.eclipse.xtext.common.Terminals

import "ch.hilbri.assist.dse.model" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "ch.hilbri.assist.model" as model

AssistModelDSE: 
	'Global' 	'{' 
					'System' '=' systemName=STRING ';' 
	'}'

	'Hardware' 	'{'	
					(compartments+=Compartment)*
	'}'

	'Software' '{'
					(applications+=Application)* 
					(applicationAlternatives+=ApplicationAlternatives)*
	'}'

	('Restrictions' '{'
					(dislocalityRelations+=DislocalityRelation)*
					(colocalityRelations+=ColocalityRelation)*
					(restrictionAlternatives+=RestrictionAlternatives)*
	'}')?
	
	('Candidates' '{'
					(explorationCandidates+=ExplorationCandidate)*
	'}')?
;

/* HARDWARE */

Compartment returns model::Compartment: 
	'Compartment' name=ID '{'
		('Manufacturer' 	'=' manufacturer=STRING 	';')?
		('PowerSupply' 	'=' powerSupply=STRING 	';')?
		(boxes+=BoxDSE)+
		('Properties' '{'	(metricParameters+=MetricParameter)* '}')?
	'}'
;

BoxDSE:
	'Box' name=ID '{'
		('Manufacturer' 			'=' manufacturer=STRING ';')?
		(boards+=Board)*
		(boardAlternatives+=BoardAlternatives)*
		('Properties' '{'	(metricParameters+=MetricParameter)* '}')?
	'}'
;
	
Board returns model::Board:
	'Board' name=ID '{'
		('Manufacturer' 			'=' manufacturer=STRING ';')?
		('Type' 					'=' boardType=STRING ';')?
		('PowerSupply' 			'=' powerSupply=STRING ';')?
	    ('DesignAssuranceLevel' 	'=' assuranceLevel=DesignAssuranceLevelType';')?
		('RAM'  					'=' ramCapacity=INT ';')?
		('ROM'  					'=' romCapacity=INT ';')?
		(processors+=Processor)+
		(ioAdapters+=IOAdapter)*
		('Properties' '{'	(metricParameters+=MetricParameter)* '}')?
	'}'
;

Processor returns model::Processor:
	'Processor' name=ID '{'
	    ('Manufacturer' 			'=' manufacturer=STRING ';')?
	    ('Type'         			'=' processorType=STRING ';')?
		(cores+=Core)+
		('Properties' '{'		(metricParameters+=MetricParameter)* '}')?
	'}';

Core returns model::Core:
	'Core' name=ID '{' 
		('Capacity' 					'=' capacity=INT ';')?
		('Architecture' 				'=' architecture=STRING ';')?
		('Properties' '{'	(metricParameters+=MetricParameter)* '}')?
	'}';

IOAdapter returns model::IOAdapter:
	'Adapter' name=ID '{' 	
	 	'Type' '=' adapterType=IOAdapterType ';'
	 	'Count' '=' totalCount=INT ';'
	'}' 
;

BoardAlternatives: 
	'Alternatives' '{' 
		(alternatives+=BoardAlternative)+
	'}'
;

BoardAlternative: 
	'Alternative' name=ID '{'
		(boards+=Board)*
	'}'
;

/* SOFTWARE */
/* ******** */

Application returns model::Application:
	'Application' name=ID '{'
	    ('CriticalityLevel' 		'=' criticalityLevel=DesignAssuranceLevelType ';')?
		('Developer'		 		'=' developedBy=STRING ';')?
		('RestrictDeploymentTo' '{' restrictMappingToHardwareElements+=[model::HardwareElement|QualifiedName] (',' restrictMappingToHardwareElements+=[model::HardwareElement|QualifiedName])* '}' ';')?
		(tasks+=Task)+
		('Properties' 			'{'	(metricParameters+=MetricParameter)* '}')?
	'}';

Task returns model::Task:
	'Task' name=ID '{'
		('CoreUtilization' 			'=' coreUtilization=INT ';')? 
 		('RequiredRAM' 				'=' ramUtilization=INT ';')? 
		('RequiredROM'				'=' romUtilization=INT ';')? 
		(ioAdapterRequirements+=IOAdapterRequirement)*
		('RestrictDeploymentTo'	'{' restrictMappingToHardwareElements+=[model::HardwareElement|QualifiedName] (',' restrictMappingToHardwareElements+=[model::HardwareElement|QualifiedName])* '}' ';')? 
		('Properties' 			'{'	(metricParameters+=MetricParameter)* '}')?
	'}';

IOAdapterRequirement returns model::IOAdapterRequirement:
	'Requires' 	requiredAdapterCount=INT 
				adapterType=IOAdapterType 'adapter' 
  			 	(isExclusiveOnly?='exclusive' | isSharedAllowed?='shared') 
	';' 
;

ApplicationAlternatives: 
	'Alternatives' '{'
		(alternatives+=ApplicationAlternative)+
	'}'
;


ApplicationAlternative: 
	'Alternative' name=ID  '{'
		(applications+=Application)*
	'}'
;

/* RELATIONEN */
/* ********** */

RestrictionAlternatives: 
	'Alternatives' '{'
		(alternatives+=RestrictionAlternative)+
	'}'
	;

RestrictionAlternative: 
	'Alternative' name=ID '{'
		(dislocalityRelations+=DislocalityRelation)*
		(colocalityRelations+=ColocalityRelation)*		
	'}'
;

DislocalityRelation returns model::DislocalityRelation:
	applications+=[model::Application] (',' applications+=[model::Application])* 'dislocal' 'up' 'to' hardwareLevel=HardwareArchitectureLevelType ';';
	
ColocalityRelation returns model::ColocalityRelation:
	applications+=[model::Application|QualifiedName] (',' (applications+=[model::Application]))* 'on' 'same' 	hardwareLevel=HardwareArchitectureLevelType ';'; 


/* Exploration Candidates */
/* ********************** */

ExplorationCandidate returns ExplorationCandidate:
	name=ID '{'
		('Boards' 		'=' boardAlternatives+=[BoardAlternative] 				(',' boardAlternatives+=[BoardAlternative])* 				';')?
		('Applications' '=' applicationAlternatives+=[ApplicationAlternative] 	(',' applicationAlternatives+=[ApplicationAlternative])* 	';')?
		('Restrictions' '=' restrictionAlternatives+=[RestrictionAlternative] 	(',' restrictionAlternatives+=[RestrictionAlternative])* 	';')?
	'}' 
;

/* Generic Stuff */
/* ************* */

MetricParameter returns model::MetricParameter:  
	name=STRING '=' value=INT ';'
;

QualifiedName:
	ID ('.' ID)*
;

/* ENUMS */
/* ***** */

enum HardwareArchitectureLevelType returns model::HardwareArchitectureLevelType:
	CORE 		= 'Core'		|
	PROCESSOR 	= 'Processor'	|
	BOARD		= 'Board'		|
	BOX			= 'Box'			|
	COMPARTMENT	= 'Compartment'
;

enum DesignAssuranceLevelType returns model::DesignAssuranceLevelType: 
	NONE= 'None' 	|
	QS	= 'QS'	 	|
	D	= 'D'		|
	C	= 'C'		|
	B	= 'B'		|
	A 	= 'A'  
;

enum IOAdapterType returns model::IOAdapterType:
	CAN 		= 'Can'					|
	ELB			= 'Enhanced Local Bus'	|
	ETH			= 'Ethernet'			|
	ANALOG 		= 'Analog'				|
	DIGITAL		= 'Digital'				|
	SERIAL		= 'Serial'				;

