grammar ch.hilbri.assist.mapping.dsl.MappingDSL with org.eclipse.xtext.common.Terminals

import "ch.hilbri.assist.model"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AssistModel: 
    'Global' '{' 
                    'System' '=' systemName=STRING ';' 
    '}'

    'Hardware' '{'    
                    (compartments+=Compartment)*
    '}'

    'Software' '{'
                    (applications+=Application)* 
    '}'

    ('Restrictions' '{'
                    (dislocalityRelations+=DislocalityRelation)*
                    (colocalityRelations+=ColocalityRelation)*
                    (dissimilarityRelations+=DissimilarityRelation)*
    '}')?
;

// Properties
ManufacturerProperty:           'Manufacturer'          '=' value=STRING                    ';';
PowerSupplyProperty:            'PowerSupply'           '=' value=STRING                    ';';
BoardTypeProperty:              'Type'                  '=' value=STRING                    ';';
RAMCapacityProperty:            'RAM'                   '=' value=INT                       ';';
ROMCapacityProperty:            'ROM'                   '=' value=INT                       ';';     
DesignAssuranceLevelProperty:   'DesignAssuranceLevel'  '=' value=DesignAssuranceLevelType  ';';
ProcessorTypeProperty:          'Type'                  '=' value=STRING                    ';';
CoreCapacityProperty:           'Capacity'              '=' value=INT                       ';';
CoreArchitectureProperty:       'Architecture'          '=' value=STRING                    ';';

CustomIntProperty:              'Custom' 'Property' name=STRING '=' value=INT               ';';
CustomStringProperty:           'Custom' 'Property' name=STRING '=' value=STRING            ';';

// Hardware Tree
Compartment: 
    'Compartment' name=ID '{'
        (   
            properties+=ManufacturerProperty            | 
            properties+=PowerSupplyProperty             |
            properties+=CustomIntProperty               |
            properties+=CustomStringProperty            |
            features+=Feature                           |
            boxes+=Box                                                                                                  
        )*
    '}'
;

Box:
    'Box' name=ID   '{'
        (
            properties+=ManufacturerProperty            |
            properties+=CustomIntProperty               |
            properties+=CustomStringProperty            |
            features+=Feature                           |
            boards+=Board
        )*
    '}'
;
    
Board:
    'Board' name=ID   '{'
        (
            properties+=ManufacturerProperty            |
            properties+=PowerSupplyProperty             |
            properties+=BoardTypeProperty               |
            properties+=RAMCapacityProperty             |
            properties+=ROMCapacityProperty             |
            properties+=DesignAssuranceLevelProperty    |
            properties+=CustomIntProperty               |
            properties+=CustomStringProperty            |
            features+=Feature                           |
            processors+=Processor  
        )*
    '}'
;

Processor:
    'Processor' name=ID   '{'
        (
            properties+=ManufacturerProperty            |
            properties+=ProcessorTypeProperty           |
            properties+=CustomIntProperty               |
            properties+=CustomStringProperty            |
            features+=Feature                           |
            cores+=Core
        )*
    '}';

Core:
    'Core' name=ID '{'
        (
            properties+=CoreCapacityProperty            |
            properties+=CoreArchitectureProperty        |
            properties+=CustomIntProperty               |
            properties+=CustomStringProperty            |
            features+=Feature                           
        )* 
    '}';

/* Features provide by the hardware */
Feature:
    FeatureSimple | FeatureSyncAccess | FeatureExclAccess;

FeatureSimple returns Feature:
    'Provides' shared?='shared' 'feature' name=STRING ';';

FeatureSyncAccess returns Feature:
    'Provides' shared?='shared' 'feature' name=STRING synchronizedAccess?='with' 'synchronized' 'access' ';';
    
FeatureExclAccess returns Feature:
    'Provides' units=INT 'of' 'exclusive' 'feature' name=STRING ';';


/* SOFTWARE */
Application:
    'Application' name=ID '{'
        ('CriticalityLevel'          '=' criticalityLevel=DesignAssuranceLevelType ';')?
        ('Developer'                 '=' developedBy=STRING ';')?
        ('RestrictDeploymentTo' '{' 
                restrictMappingToHardwareElements+=[HardwareElement|QualifiedName] (',' restrictMappingToHardwareElements+=[HardwareElement|QualifiedName])* 
          '}' ';' )?
        (tasks+=Task)+
//        ('Properties' '{' (customProperties+=CustomStringProperty | customProperties+=CustomIntProperty)* '}' )?
    '}';

Task:
    'Task' name=ID '{'
        ('CoreUtilization'             '=' coreUtilization=INT ';')? 
        ('RequiredRAM'                 '=' ramUtilization=INT  ';')? 
        ('RequiredROM'                 '=' romUtilization=INT  ';')? 
        ('RestrictDeploymentTo' '{' 
                  restrictMappingToHardwareElements+=[HardwareElement|QualifiedName] (',' restrictMappingToHardwareElements+=[HardwareElement|QualifiedName])* 
            '}' ';'
        )? 
        (featureRequirements+=FeatureRequirement)*
//        ('Properties' '{' (customProperties+=CustomStringProperty | customProperties+=CustomIntProperty)* '}' )?
    '}';

/* General Feature Requirements - see #38 */
FeatureRequirement: 
    SharedFeatureRequirement      |
    ExclusiveFeatureRequirement;

SharedFeatureRequirement returns FeatureRequirement:
    'Requires' shared?='shared' hardwareLevel=HardwareArchitectureLevelType 'feature' name=STRING ';';

ExclusiveFeatureRequirement returns FeatureRequirement:
    'Requires' units=INT 'of' 'exclusive' hardwareLevel=HardwareArchitectureLevelType 'feature' name=STRING ';';


/* Safety Restrictions / Relations */

DislocalityRelation:
	softwareElements+=[SoftwareElement|QualifiedName] (',' softwareElements+=[SoftwareElement|QualifiedName])*
    'dislocal' 'up' 'to' hardwareLevel=HardwareArchitectureLevelType ';';
    
ColocalityRelation:
   	softwareElements+=[SoftwareElement|QualifiedName] (',' softwareElements+=[SoftwareElement|QualifiedName])*
   	'on' 'same' hardwareLevel=HardwareArchitectureLevelType ';'; 

DissimilarityRelation: 
    softwareElements+=[SoftwareElement|QualifiedName] (',' softwareElements+=[SoftwareElement|QualifiedName])*
    'dissimilar' 'based' 'on'
    dissimilarityClause=DissimilarityClause ';';

DissimilarityClause:  
    DissimilarityEntry | 
    (=>DissimilarityDisjunction | DissimilarityConjunction);  

DissimilarityEntry: 
    dissimilarityAttribute=DissimilarityAttributes;

DissimilarityDisjunction: 
    '(' dissimilarityClauses+=DissimilarityClause ( 'OR' dissimilarityClauses+=DissimilarityClause)+     ')' ;

DissimilarityConjunction: 
    '(' dissimilarityClauses+=DissimilarityClause ( 'AND' dissimilarityClauses+=DissimilarityClause)+    ')';


enum DissimilarityAttributes:
    COMPARTMENT_MANUFACTURER    = 'Compartment.Manufacturer'          |
    COMPARTMENT_POWERSUPPLY     = 'Compartment.PowerSupply'           |
    BOX_MANUFACTURER            = 'Box.Manufacturer'                  |
    BOARD_MANUFACTURER          = 'Board.Manufacturer'                |
    BOARD_BOARDTYPE             = 'Board.Type'                        |
    BOARD_POWERSUPPLY           = 'Board.PowerSupply'                 |
    BOARD_ASSURANCELEVEL        = 'Board.DesignAssuranceLevel'        |
    PROCESSOR_MANUFACTURER      = 'Processor.Manufacturer'            |
    PROCESSOR_PROCESSORTYPE     = 'Processor.Type'                    |
    CORE_ARCHITECTURE           = 'Core.Architecture'
;

QualifiedName:
    ID ('.' ID)*
;

/* ENUMS */
enum HardwareArchitectureLevelType:
    CORE         = 'Core'           |
    PROCESSOR    = 'Processor'      |
    BOARD        = 'Board'          |
    BOX          = 'Box'            |
    COMPARTMENT  = 'Compartment'
;

enum DesignAssuranceLevelType: 
    NONE = 'None'     |
    QS   = 'QS'       |
    D    = 'D'        |
    C    = 'C'        |
    B    = 'B'        |
    A    = 'A'  
;
